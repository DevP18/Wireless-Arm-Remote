// arduino mega connecting with the nRF24L01 to send glove data to the Raspberry pi
#include <SPI.h>
#include <RF24.h>
#include <Wire.h>

RF24 radio(9, 10);   // CE, CSN
const byte address[6] = "HAND1";

struct DataPacket {
  int thumb;
  int index;
  int middle;
  int ring;
  int pinky;
  int wrist;
};

DataPacket data;

#define MPU_ADDR 0x68
int16_t GyX;

void setup() {
  Serial.begin(9600);
  Wire.begin();

  radio.begin();
  radio.openWritingPipe(address);
  radio.setPALevel(RF24_PA_LOW);
  radio.stopListening();

  // Wake MPU6050
  Wire.beginTransmission(MPU_ADDR);
  Wire.write(0x6B);
  Wire.write(0);
  Wire.endTransmission(true);
}

void loop() {
  data.thumb  = analogRead(A0);
  data.index  = analogRead(A1);
  data.middle = analogRead(A2);
  data.ring   = analogRead(A3);
  data.pinky  = analogRead(A4);

  readMPU();
  data.wrist = GyX;

  radio.write(&data, sizeof(DataPacket));
  delay(20);
}

void readMPU() {
  Wire.beginTransmission(MPU_ADDR);
  Wire.write(0x43);
  Wire.endTransmission(false);
  Wire.requestFrom(MPU_ADDR, 2, true);
  GyX = Wire.read() << 8 | Wire.read();
}
